;=== 松下仕様12ドット漢字ROM検索(FS-CM1/FS-4600F/FS-4700F) ===
;(C)1998 GIGAMIX/Goripon
;01/07/11
;	r010711a	初版

	.z80
	cseg

;松下仕様12ドット漢字があるかどうか調べる
;ret:	A       = bit0 第一水準
;                 bit1 第二水準
;	Zフラグ = あれば nz, 無ければ z
;break:		af,bc,de

SRCHM12::
	ld	e,0
	in	a,(40h)
	cpl
	ld	d,a
	ld	a,0F7h
	out	(040h),a
	in	a,(40h)
	cp	08h
	jr	z,l1found
	ld	a,01h
	out	(42h),a
	xor	a
	out	(47h),a
	out	(48h),a
	in	a,(49h)
	ld	c,a
	in	a,(49h)
	or	c
	jr	nz,l1notfound
	ld	a,0Eh
	out	(47h),a
	ld	a,5Fh
	out	(48h),a
	ld	bc,0800h
sumloop:
	in	a,(49h)
	add	a,c
	ld	c,a
	djnz	sumloop
	cp	4Ch
	jr	nz,l1notfound
l1found:
	set	0,e
l1notfound:
	in	a,(41h)
	cp	08h
	jr	z,l2found
	ld	a,02h
	out	(42h),a
	ld	a,25h
	out	(47h),a
	ld	a,5Eh
	out	(48h),a
	in	a,(49h)
	cp	41h
	jr	nz,l2notfound
	in	a,(49h)
	cp	42h
	jr	nz,l2notfound
l2found:
	set	1,e
l2notfound:
	ld	a,d
	out	(40h),a
	ld	a,e
	or	a
	ret

	end
